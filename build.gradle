buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    jcenter()
    mavenCentral()
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "im.actor.cloud.free"
        minSdkVersion 15
        targetSdkVersion 24
        
        if (System.getenv("BUILD_VERSION") != null) {
            // Convering 1.8.3(123) -> ["1.8.3", "123"]
            def buildVersions = System.getenv("BUILD_VERSION").split("\\(")
            buildVersions[1] = buildVersions[1].substring(0, buildVersions[1].length() - 1)
            versionName buildVersions[0]
            versionCode buildVersions[1].toInteger()
            setProperty("archivesBaseName", "Actor-Open-$versionName-$versionCode")
        } else {
            versionName "1.0.0"
            versionCode 1000000
        }
    }
    
    if (project.hasProperty("ACTOR_KEYSTORE")) {
        signingConfigs {
            release {
                storeFile file(ACTOR_KEYSTORE)
                storePassword ACTOR_KEYSTORE_PASS
                keyAlias ACTOR_KEYSTORE_ALIAS
                keyPassword ACTOR_KEYSTORE_ALIAS_PASS
            }
        }
    }
    
    buildTypes {
        release {
            if (project.hasProperty("ACTOR_KEYSTORE")) {
                signingConfig signingConfigs.release
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'im.actor:android-sdk:3.0.396'
}
